title: Potira
tags: 
colorID: 0
position: 421,163
---
<<if ($firstDay is true) and (not visited("Potira.PrimeiroDia"))>>
	[[Potira.PrimeiroDia]]
<<endif>>

<<if ($firstDay is true) and ($classDone is true)>>
	[[Potira.PrimeiroDiaDepoisAula]]
<<endif>>

[[Potira.Padrao]]
===

title: Potira.Padrao
tags: 
colorID: 0
position: 421,163
---
<<if $isMorningTime is true>>
	Potira: Bom dia Porã, tenha um ótimo dia.
<<elseif $isAfternoonTime is true>>
	Potira: Olha como as flores estão lindas.
	Potira: Vou começar a arrumá-las para as encomendas.
<<elseif $isNightTime is true>>
	Potira: *Boceja*
	Potira: Que dia longo, estou cansada.
	Potira: Boa noite, meu xodó.
<<endif>>
===

title: Potira.Adeus
tags: 
colorID: 0
position: 421,163
---
Potira: Tchau, meu xodó.
===

title: Potira.PrimeiroDia
tags: 
colorID: 0
position: 500,163
---
Potira: Bom dia! Vamos acordar!? Você não pode perder seu primeiro dia de aula?
Porã: Bom dia, mãe. Ainda estou com muito sono, queria dormir mais um pouco... 
Porã: *Uaaaah!*
Potira: Não fique assim Porã, hoje é seu primeiro dia de aula no 7º ano. 
Potira: Não vai querer perder o primeiro dia de aula né?
-> *Uaaaah!* Eu não poderia só faltar o primeiro dia de aula não?
	Potira: Mas por que queres falta logo hoje?
	Porã: Todo mundo fica me olhando, como se eu fosse <b>diferente</b>, não gosto disso.
	Potira: Oh meu filhote, você não é diferente. Você é tão Lobo-guará quanto qualquer outro.
	Potira: Você vai ficar bem hoje.
	Porã: Tá bom mãe, eu vou hoje.
-> Não, eu sei que você não gostaria disso, mãe.
	Potira: Ainda bem que você sabe hahaha.

Potira: Estou muito orgulhosa de você, meu xodó. Você tá crescendo muito rápido.
Potira: Alguma expectativa para esse ano?
-> *Suspira* Só espero que não seja igual ao ano passado.
	Porã: Parecia que o 6º não ia terminar.
	Potira: Você está crescendo rápido. Esse ano irá passar em um piscar de olhos.
	Potira: Quando eu tinha a sua idade eu desejava que essa época nunca acabasse.
	Potira: Era muito bom andar pela vila, brincando sem nenhuma preocupação.
	Porã: Se eu tivesse alguém com quem brincar...
-> Hmmm... *pensativo* não tenho nada em mente agora.
	Potira: Anime-se Porã, você tem que ser mais otimista, tente pensar em algo.
	Porã: *Porã pensa mais um pouco* Acho que fazer amizades seria bom…
	Potira: Olha só, foi difícil pensar? É um bom começo.
-> Não sei, mas estou com uma sensação boa.
	Potira: Você vai aprender coisas novas e descobrir novos mundos. Talvez até crie laços e faça novas amizades.
	Porã: Estou um pouco assustado e ansioso, mas com um bom pressentimento.
	Potira: É normal se sentir assim, mas vai dar tudo certo. Você não está sozinho.
	Porã: Eu não estaria sozinho se tivesse um amigo...
	
Potira: Não fique triste assim, filhote.
Potira: Soube que hoje uma família se mudou para vila. E que eles também tem um filhote da sua idade.
Potira: Talvez vocês se conheçam hoje na escola. Vocês podem ser amigos.
Porã: Você já os conheceu?
Potira: Ainda não. Mas agora de manhã vou dar uma passadinha na casa deles.
Porã: Você realmente acha que eu vou conseguir um amigo?
Potira: Claro, que sim! Você é o filhote mais fofo da vila.
Porã: *Porã fica um pouco feliz.*
Porã: Então, o que eu devo fazer? Eu deve ser eu mesmo?
Potira: *Potira fica curisosa.*
Potira: Como assim? Mas é claro que você deve ser você mesmo!
Porã: *Porã fica com um sorriso no rosto.*
Porã: Sério!?
Potira: *Potira fica preocupada e começa a falar baixinho*
Potira: Mas Porã, você me promete que vai se <b>comportar</b> né?
Porã: *Porã fica triste.*
Porã: ...
Porã: Me comportar?
Potira: *Potira fica com um sorriso nervoso.*
Potira: Sim, igual ao meu <b>lobinho</b> que você é.
Potira: Você não deveria expor <b>isso</b> para os outros.
Porã: ...
Potira: Não fique triste, meu amor. Você sabe que é para o seu bem né?
Potira: Não queremos seu pai descobrindo isso.
Porã: Já entendi, mãe...
Potira: Agora vá para sua aula. Eu te amo e boa aula!
Porã: Obrigado mãe, também te amo...

[[Potira.PrimeiroDia2]]
===

title: Potira.PrimeiroDia2
tags: 
colorID: 0
position: 500,163
---
<<deactivateContainer _dialogue/DialogueRunner>>
<<showNPC SceneManager Piatã 0>>
<<wait 0.1>>
<<moveTo NPCs/Piatã 1 1.5>>
<<wait 1.5>>
<<moveTo NPCs/Piatã 2 1>>
<<wait 1>>
<<moveTo NPCs/Piatã 3 1.5>>
<<wait 1.5>>
Piatã: Bom dia Porã, bom primeiro dia de aula.
Porã: Bom dia pai, obrigado!
Piatã: Estarei te esperando na nossa plantação de tarde. Até mais tarde.
Porã: ...
Porã: Então, tá. Tchau...
Piatã: Tchau.
<<set $piataKitchen to true>>
<<set $firstClass to true>>
<<set $isClassTime to true>>
<<startQuest NPCs/Potira/Quest-PrimeiroDia 6>>
===

title: Potira.PrimeiroDiaDepoisAula
tags: 
colorID: 0
position: 500,163
---
<<if not visited("Potira.PrimeiroDiaDepoisAula")>>
	Potira: Vai, me conta. Como foi o primeiro dia de aula?
	-> Tudo bem… como sempre...
		Porã: Não aconteceu nada demais.
		Potira: Sabia que meu <b>lobinho</b> se daria bem no seu primeiro dia de aula.
		Porã: *Porã começa a ficar chateado*
		Potira: Aconteceu mais alguma coisa hoje?
		Porã: Não...
		Potira: Entendo, mas espero que você não esteja escondendo nada de mim, viu?
		Porã: ...
		<<set $potiraFriendship to ($potiraFriendship - 1)>>
		
	-> Eu sabia que ia ser igual ao ano passado.
		Potira: Como assim, Porã?
		Porã: Nada mudou, tudo continua igual.
		Potira: As coisas já estão mudando! Olha o <b>lobinho</b> que você está se tornando.
		Porã: *Porã começa a ficar chateado*
		-> *Contar sobre a atividade de biologia de hoje?*
			Porã: Hoje teve aula de biologia.
			Porã: Aprendemos um pouco mais sobre a fauna de onde moramos.
			Potira: Que legal filhote! E você se deu bem na aula de hoje?
			-> Sim, tirei nota boa.
				Potira: *Potira esboça um sorriso*
				Potira: Muito bem! Estou orgulhosa de você!
				<<if $biologyDayResults < 7.0>>
					Porã: *Porã fica nervoso*
					<<set $potiraFriendship to ($potiraFriendship - 1)>>
				<<else>>
					Porã: *Porã da um sorriso singelo*
					<<set $potiraFriendship to ($potiraFriendship + 1)>>
				<<endif>>
			-> Não, eu tirei {$biologyDayResults}, desculpa... <<if $biologyDayResults < 7.0>>
				Potira: Own meu amor! Ta tudo bem, você precisa de ajuda do seu pai?
				Porã: Não...
				Porã: *Porã fica receoso*
				Porã: Eu consigo me virar sozinho. Ta tudo bem.
				Potira: *Potira esboça um sorriso*
				Potira: Eu confio em você.
				<<set $potiraFriendship to ($potiraFriendship + 1)>>
		-> *Não contar sobre a atividade*

	Porã: Mãe...  posso te perguntar uma coisa?
	Potira: Claro! O que você quiser!
	-> Por quê todo mundo insite em me dizer que eu devo ser um <i><b>lobinho</i></b>?
		Potira: Ué Porã, porque você ainda é uma criança hahaha!

		Porã: Não foi isso que eu quis dizer.
		Porã: Eu estou perguntando por quê eu devo ser um Lobo-guará?

		Potira: Mas não é obvio? Você nasceu um Lobo-guará igual a eu e seu pai.
		Potira: É o normal que as pessoas esperem isso de você.

		Porã: Mas e se eu...
		Porã: *Porã fala o resto tão baixo que a mãe não escuta*

		Potira: Ham?

		Porã: Nada não...

	-> Nada não, esquece...

	Potira: E como vai Acir? Vocês estão bem?
	Porã: Acir? Por que pergunta?
	Potira: Porque vocês são amigos, ele vinha aqui em casa todo dia quando criança pra brincar com você.
	Porã: Mãe, isso faz muito tempo... Acir não vem aqui tem anos.
	Potira: Ah mas isso não significa que vocês deixaram de ser amigos.
	Potira: Acir é um ótimo exemplo de Onça-pintada, vai te fazer muito bem vocês andarem juntos. Igual quando vocês eram crianças.
	Porã: Quando éramos crianças, tudo era mais fácil.. Acir mudou, está distante…
	Potira: … 
	Potira: *Potira não tem o que comentar.*
	Porã: Queria que fosse igual a antigamente...
	<<setDayShift _app 1>>
	<<flashbackScene _loadLevel FlashBackCume1 SaidaFlashBackCume1 1>>
<<else>>
	[[Potira.Padrao]]
<<endif>>
===

title: Potira.PrimeiroDiaDepoisAula2
tags: 
colorID: 0
position: 500,163
---
Potira: Porã?
<<wait 1>>
Potira: Meu xodó?
<<wait 1>>
Potira: Acorda!

<<popSymbol Porã exclamation>>
<<wait 1>>
Porã: *Porã leva um susto com o grito da mãe*
Porã: Oi, mãe. O que foi?

Potira: Parecia que você estava em transe, eu tava falando com você e você não me respondia.

Porã: Ah, me desculpa. Eu tava...
Porã: Pensando demais...
Porã: Mas o que você estava falando mesmo?

Potira: Hahaha, você sempre foi assim né?
Potira: Eu te perguntei sobre a Moara, vocês se encontraram?

Porã: Como você sabe o nome de Moara?

Potira: Eita que você anda dormindo acordado mesmo ein? Hahaha
Potira: Eu falei que ia passar na casa dos novos moradores hoje de manhã.

Porã: Ah! É mesmo. E como eles são?

Potira: Primeiro você me responda. Hehe.
Potira: Você fez amizade com Moara?

Porã: Não, mãe. Eu acabei de conhecer…
Porã: A gente nem conversou. Pode ser que Moara nem queira mais fazer amizade comigo.

Potira: Por que você diz isso, filhote?
-> Aconteceu algo na escola hoje, depois da aula…
	Porã: Quando eu tava indo pro meu armário guardar minha coisas.
	Porã: Algumas pessoas ficaram falando alto e rindo de mim no corredor.
	Porã: Ai Moara viu tudo. Acho difícil querer alguma amizade comigo depois disso.

	Potira: *Potira fica sem palavras*
	Potira: Você não fez nada para que eles ficassem rindo de você?

	Porã: *Porã fica triste*
	<<wait 1>>
	Porã: *Porã respira fundo* 
	Porã: Não, mãe…

	Potira: *Potira nota que falou algo errado*
	Potira: Não se preocupe meu xodó, um dia isso tudo vai se resolver.
	Potira: Nem que eu mesma tenha que resolver.
	Potira: Pode contar comigo. Eu sou sua mãe, estou aqui para isso.

	Porã: *Porã fica menos triste*
	Porã: Obrigado mãe.
	<<set $potiraFriendship to ($potiraFriendship + 1)>>
	<<set $potiraKnowsSchoolLocker to true>>
	
-> Nada não, só intuição mesmo…
	Porã: Nada não, esquece hehe.
	Porã: *Porã dá uma risadinha de nervoso*

	Potira: Ah, então tá certo.

Potira: Mas não seja pessimista filhote.
Potira: Ainda é o primeiro dia, tenho certeza que você e Moara ainda serão grandes amigos.

Porã: Espero…
Porã: *Não quero mais falar disso, vou mudar de assunto*
Porã: E como foi lá na casa dos novos moradores?

Potira: Ah! Hehe…
Potira: *Potira fica nervosa*
Potira: Eles parecem bem… 
Potira: Animados…

Porã: *Foi a mesma coisa que eu pensei de Moara*

Potira: Mas eles parecem ser pessoas boas, acho que até mesmo bons exemplos para você.

Porã: …

Potira: Amanhã depois da aula você deveria ir na casa deles, conhecê-los.

Porã: Eu não poderia ir agora?

Potira: Não mesmo, lobinho. Você esqueceu que seu pai pediu ajuda na fazenda hoje de tarde?

Porã: Ah, eu tinha esquecido.
Porã: Então... Eu vou agora me encontrar com ele.

Potira: Está certo, meu xodó. Divirta-se lá, e lembre-se: ele pode parecer grosso, mas ele só quer seu bem.
Potira: E vai fazer bem para vocês dois, terem momentos juntos, depois daquele dia.

Porã: *Porã começa a ficar nervoso*

Potira: Então tenha paciência com ele, certo?

Porã: Certo…
Porã: *Eu tenho que ter paciência com todo mundo, mas ninguém tem paciência comigo.*

<<if $potiraKnowsSchoolLocker is true>>
	Potira: Porã, mais uma coisa.

	Porã: Oi, mãe. O que foi?

	Potira: Não tenha medo de me contar as coisas. Se alguém lhe incomodar de novo, você pode vir me contar.

	Porã: Está certo, obrigado mãe.
	Porã: *Porã fica feliz*
<<endif>>

<<set $piataFarmTalk to true>>
<<startQuest NPCs/Potira/Quest-FazendaPiata 8>>
<<placeDone Potira-PrimeiroDia>>
===